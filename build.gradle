buildscript {
    ext {
        depManagementVersion = "1.0.6.RELEASE"
        kotlin_version = '1.3.21'
        spring_boot_version = "2.1.3.RELEASE"
        spring_security_oauth2 = "2.3.4.RELEASE"
        spring_security_jwt = "1.0.10.RELEASE"
        junit_platform_version = "1.4.0"
        junit_jupiter_version = "5.4.0"
        spring_fox_version = "2.9.2"
        lombokVersion = "1.16.22"
    }
    repositories {
        maven { url "${artifactory_contextUrl}/plugins-release" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version" // Required for Kotlin integration
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version" // See https://kotlinlang.org/docs/reference/compiler-plugins.html#spring-support
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
    }
}

allprojects {
    group 'com.sergeybannikov'
    version '1.0-SNAPSHOT'

    apply plugin: 'kotlin' // Required for Kotlin integration
    apply plugin: "kotlin-spring" // https://kotlinlang.org/docs/reference/compiler-plugins.html#spring-support
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceSets {
        main {
            java { srcDirs = ["src/main"] }
            resources { srcDirs = ["src/main/resources"] }
        }
        test {
            java {
                srcDirs = ["src/test"]
            }
            resources {
                srcDirs = ["src/test/resources"]
            }
        }
    }

    dependencyManagement {
        imports {
        mavenBom "org.springframework.security:spring-security-bom:5.1.4.RELEASE"
        mavenBom "org.springframework.cloud:spring-cloud-vault-dependencies:2.1.0.RELEASE"
        }
    }


    dependencies {
        implementation 'io.vavr:vavr:0.9.2'
        implementation 'io.vavr:vavr-jackson:0.9.2'
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.springframework.boot:spring-boot-starter-parent:$spring_boot_version"
        implementation "org.springframework.boot:spring-boot-starter-web:$spring_boot_version"
        implementation "org.springframework.boot:spring-boot-configuration-processor:$spring_boot_version"
        implementation "org.springframework.cloud:spring-cloud-starter-vault-config"
        implementation "org.springframework.boot:spring-boot-starter-logging:$spring_boot_version"
        implementation "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"

        implementation "org.jboss.resteasy:jaxrs-api:3.0.12.Final"
        implementation 'org.jboss.resteasy:resteasy-client:3.1.4.Final'
        implementation 'org.jboss.resteasy:resteasy-jackson2-provider:3.1.4.Final'

        testImplementation ("org.springframework.boot:spring-boot-starter-test:$spring_boot_version"){
            exclude group: "com.vaadin.external.google", module:"android-json"
            exclude group: "org.springframework.boot", module:"spring-boot-starter-logging"
            exclude group: 'junit', module: 'junit'

        }
        testRuntime "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"
        testRuntime "org.junit.vintage:junit-vintage-engine:$junit_jupiter_version"
        testImplementation "org.junit.platform:junit-platform-launcher:${junit_platform_version}"
        testImplementation "org.junit.platform:junit-platform-commons:${junit_platform_version}"
        testImplementation 'com.github.sbrannen:spring-test-junit5:1.4.0'

    }

    repositories {
        maven { url "${artifactory_contextUrl}/libs-release" }
        maven { url "${artifactory_contextUrl}/libs-snapshot" }
    }

    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
            excludeEngines 'custom-engine'
        }
        systemProperty "file.encoding", "utf-8"
    }

    task testFast(type: Test) {
        useJUnitPlatform {
            excludeTags 'slow'
        }

        systemProperty "file.encoding", "utf-8"
    }

    task testIntegration(type: Test) {
        useJUnitPlatform {
            includeTags 'slow'
        }

        systemProperty "file.encoding", "utf-8"
    }
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

}

